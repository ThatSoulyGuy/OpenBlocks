plugins {
    id 'dev.architectury.loom' version "${architectury_loom_version}" apply false
    id 'architectury-plugin' version "${architectury_plugin_version}"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'

    base {
        archivesName = "${rootProject.mod_id}-${project.name}"
    }

    version = rootProject.mod_version
    group = rootProject.mod_group

    repositories {
        maven { url 'https://maven.architectury.dev/' }
        maven { url 'https://maven.fabricmc.net/' }
        maven { url 'https://maven.neoforged.net/releases/' }
        maven { url 'https://maven.shedaniel.me/' }
        maven { url 'https://maven.parchmentmc.org/' }
        mavenCentral()
    }

    configurations.all {
        resolutionStrategy {
            force 'dev.architectury:architectury-transformer:5.2.88'
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment_version}@zip")
        }
    }

    java {
        sourceCompatibility = JavaVersion.toVersion(rootProject.java_version)
        targetCompatibility = JavaVersion.toVersion(rootProject.java_version)

        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release = Integer.parseInt(rootProject.java_version)
    }
}
